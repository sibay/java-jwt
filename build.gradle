plugins {
    id 'me.champeau.gradle.jmh' version '0.2.0'
}
apply plugin: 'me.champeau.gradle.jmh'
apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'signing'


sourceCompatibility = '1.8'
[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

if (!hasProperty('mainClass')) {
    ext.mainClass = ''
}

repositories {
    mavenCentral()
    mavenLocal()
}

task resolveSources << {
    def componentIds = configurations.compile.incoming.resolutionResult.allDependencies.collect { it.selected.id }
    componentIds += configurations.testCompile.incoming.resolutionResult.allDependencies.collect { it.selected.id }

    def result = dependencies.createArtifactResolutionQuery()
        .forComponents(componentIds)
        .withArtifacts(JvmLibrary, SourcesArtifact, JavadocArtifact)
        .execute()

    for (component in result.resolvedComponents) {
        component.getArtifacts(SourcesArtifact).each { println "Source artifact for ${component.id}: ${it.file}" }
    }
}

group = "de.notizwerk"
version = "3.0.0"


sourceSets {
    legacy {
        java {
          compileClasspath += sourceSets.main.output + sourceSets.main.runtimeClasspath
          runtimeClasspath = output + compileClasspath 
        }
    }
    jmh {
        java {
           compileClasspath +=  sourceSets.legacy.runtimeClasspath
        }
    }
}

dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.10'
    
    compile 'io.fastjson:boon:0.33'
    compile 'org.apache.commons:commons-lang3:3.4'
    compile 'org.bouncycastle:bcprov-jdk15on:1.52'
        
    legacyCompile "com.fasterxml.jackson.core:jackson-databind:2.6.0"
    legacyCompile "commons-codec:commons-codec:1.4"
    legacyCompile 'junit:junit:4.10'
    
    jmh "com.fasterxml.jackson.core:jackson-databind:2.6.0"
    jmh "commons-codec:commons-codec:1.4"
    jmh 'io.fastjson:boon:0.33'
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

signing {
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { deployment -> signing.signPom(deployment) }
           
            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
               authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
              authentication(userName: ossrhUsername, password: ossrhPassword)
            }            
            pom.project {
                name "Java JWT"
                description "Java implementation of JSON Web Token developed against draft-ietf-oauth-json-web-token-08."
                url "https://github.com/sibay/java-jwt"
                scm {
                    url "https://github.com/sibay/java-jwt"
                    developerConnection "scm:git:git@github.com:sibay/java-jwt.git"
                    connection "scm:git:git@github.com:sibay/java-jwt.git"
                }
                licenses {
                    license {
                        name 'The MIT License'
                        url 'http://www.opensource.org/licenses/mit-license.php'
                        distribution 'repo'
                    }
                }
                developers {
                    developer {
                      id 'sibay'
                      name 'Tarek El-Sibay'
                      email 't.elsibay@notizwerk.de'
                    }
                }
            }
        }
    }
}
// check if the new implementation generates the same output as the old one:
//
//  ./gradlew compatibilityTest
task compatibilityTest(type: Test) {
    testClassesDir = sourceSets.legacy.output.classesDir
    classpath = sourceSets.legacy.runtimeClasspath
}

// starts the benchmark to compare the speed:
// apache commons codec Base64 vs jdk Base64
// boon JSON vs jackson JSON
// old vs new jwt implementation
//
// ./gradlew jmh
//
// the report is in build/reports/jmh/human.txt
jmh {
    verbosity = "NORMAL"
    include = ".*Benchmark"
    failOnError = true
    fork = 1
    iterations = 5
    timeOnIteration = '2s' 
    warmupIterations = 2
}

project.tasks.jmh.doFirst {
    classpath += sourceSets.legacy.runtimeClasspath
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.8'
}

